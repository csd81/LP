---------------
Problem 10.
Solve the production problem, provided that for each raw material and each product, a minimal
and maximal total usage is also given that must be respected by the solution.
The usage of a raw material is the total amount consumed, and the usage of a product is the
total amount produced.
These are additional restrictions on the production mix in question. Let us see how it affects
the model formulation. The problem remained almost the same, just additional solutions must be
excluded from the feasible set. Namely, those for which any newly introduced restriction is violated.
The change only added new restrictions, so the variables, the objective and even the already
mentioned constraints and bounds may remain the same. The new limits shall be implemented by
new constraints and/or bounds. We also need to provide new options in the data sections where
these limits can be given as parameter values.
There are four limits altogether.
• Upper limit on production amount. The production amount for any product p appears as the
production[p] variable in the model. A constant upper limit for this value can easily be
implemented as a linear constraint, but an even easier way is to implement it as an upper
bound of variable production.
• Lower limit on production amount. The same as for the upper limit. Just note that there is
already a nonnegativity bound defined for the variables, and defining two lower or two upper
bounds on the same variable is forbidden in GNU MathProg.
• Upper limit on raw material consumption. The total consumption of each raw material is
already represented in the model as an expression. Actually, the only constraint in the production problem so far defines an upper limit for this expression as Storage[r] for any raw
material r. Therefore there is no need to further define upper bounds. If one wants to give
an extra upper limitation for the total consumption of a raw material, then it can be done
without modifying the model section, just by decreasing the appropriate Storage parameter
value in the data section.
• Lower limit on raw material consumption. As we noticed before, the expression already appears in a constraint. We need to implement another constraint with exactly the same expression, but expressing a minimum limit instead of the maximum.
First, we have to add the extra parameters to describe the limits.

50

5.2. INTRODUCING LIMITS

PRODUCTION PROBLEM

param Storage {r in Raw_Materials}, >=0, default 1e100;
param Consumption_Rate {r in Raw_Materials, p in Products}, >=0, default 0;
param Revenue {p in Products}, >=0, default 0;
param Min_Usage {r in Raw_Materials}, >=0, <=Storage[r], default 0;
param Min_Production {p in Products}, >=0, default 0;
param Max_Production {p in Products}, >=Min_Production[p], default 1e100;
The new Min_Usage is for the minimal total consumption for each raw material, while parameters Min_Production and Max_Production are for the lower and upper limit of production of each
product. The fourth limit parameter is the original Storage, for upper limit of raw material consumption. All these limit parameters now have sensible bounds and default values. Lower limits are
defaulted to 0, while upper limits are defaulted to 10100 , which is a number of a large magnitude
that we except not to appear in problem data. Also, the lower limits must be not larger than
the upper limits. Meanwhile, all limits must be nonnegative. Remember that these restrictions on
parameter values are valuable to prevent mistakes in the data sections but we do not expect them
to contribute to generating and solving the model if once satisfied. This is in contrast to variable
bounds, which are part of the model formulation and may affect solutions.
The variables and the objective remains the same. However, we must add three additional
constraints to tighten the search space of the model. Each of the four constraints actually correspond to the four limits mentioned, while the Material_Balance being for the upper limit of total
consumption of raw materials, this one was originally there and was left unchanged.
var production {p in Products}, >=0;
s.t. Material_Balance {r in Raw_Materials}:
sum {p in Products} Consumption_Rate[r,p] * production[p]
<= Storage[r];
s.t. Cons_Min_Usage {r in Raw_Materials}:
sum {p in Products} Consumption_Rate[r,p] * production[p]
>= Min_Usage[r];
s.t. Cons_Min_Production {p in Products}:
production[p] >= Min_Production[p];
s.t. Cons_Max_Production {p in Products}:
production[p] <= Max_Production[p];
maximize Total_Revenue: sum {p in Products} Revenue[p] * production[p];
The post-processing work to print out solution data may remain the same as for the original
model. Now, our model section is ready.
To demonstrate how it works, consider the following production problem with limits.
