---------------
Problem 15.
Given a set of food types, and a set of nutrients. Each food consists of a given, fixed ratio of the
nutrients.
We aim to arrange a diet, which is any combination of the set of food types, in any amounts.
However, for each nutrient, there is a minimum requirement that the diet must satisfy in order to
be healthy. Also, each food has its own proportional cost.
Find the healthy diet with the lowest total cost of food involved.
After the general problem definition, let us see a particular example.

Problem 16.
Solve the diet problem with the following data. There are five food types, named F1 to F5, and
there are four nutrients under focus, named N1 to N4. The contents of unit amount of each food, the
unit cost of each food, and the minimum requirement of each nutrient are shown in the following
table.

F1
F2
F3
F4
F5
Required

N1
30
20
25
13
19
2000

N2
5.2
0
2
3.6
0.1
180

N3
0.2
0.7
0.1
0
0
30

N4
0.0001
0.0001
0.0001
0.0002
0.0009
0.04

Cost
450
220
675
120
500

The diet problem is regarded as one of the first problems that raised the field of Operations
Research.
Note that dimensions are slightly different at the diet problem and the production problem. Here
we omitted fictional physical dimensions, but the scales of each nutrient suggests which are the data
that are of one dimension. In this data table, each column corresponds for a single nutrient, and
therefore it has its own unit of measure. The last column shows unit costs.
Usually, when we want to implement a model, the first thing we must decide is how our freedom
of choice would be implemented as decision variables, then how the search space can be described
by constraints, and how the objective can be calculated. These steps are essential if we first want to
decide about a real-world problem whether a mathematical programming approach is suitable for
it at all.
However, we now instead start by defining all the data that are available in the problem, and
then we will go on with the procedure above. This makes defining the variables, constraints and
objectives easier afterwards. So the first part of modeling in GNU MathProg is now defining the
sets and parameters. These are either calculated on spot, or provided afterwards in a data section
at the end of the model file or separate data file(s).
Two sets appear in the problem. One is for food types, and one is for nutrients. These sets will
be used in indexing expressions.

61

5.5. DIET PROBLEM

PRODUCTION PROBLEM

set FoodTypes;
set Nutrients;
There are three parameters available. One is for denoting food costs, obviously defined for each
food type. One parameter is for denoting minimum required amount of nutrients, defined for each
nutrient. Finally, one parameter is for the contents of food, which can be given by a unit amount
for each pair of food type and nutrient. In the particular problem, for example, each unit of F2
contains 20 units of N1, 0.7 units of N3 and 0.0001 units of N4. The default values are all set to
zero, and all these parameters are nonnegative by nature.
param Food_Cost {f in FoodTypes}, >=0, default 0;
param Content {f in FoodTypes, n in Nutrients}, >=0, default 0;
param Requirement {n in Nutrients}, >=0, default 0;
Now that all the data are defined as sets and variables, we must identify our freedom of choice.
The amounts of each food used is clearly under decision. On the other hand, if we know these
amounts, then we can easily calculate the total cost, and nutrient contents as well, so we can decide
whether the diet is healthy, and if so, how much it costs. This means that we should define a variable
denoting food consumption, for each food type separately, and no more variables are needed in the
model.
We introduce a variable named eaten to denote the amount of each food included in the diet.
This variable is obviously nonnegative, and is indexed over the set of food types. We also introduce
the auxiliary variable for the total costs so that it can be printed out easier. We could define bounds
for the auxiliary variable, but it is unnecessary as its value will be exactly calculated by a constraint.
var eaten {f in FoodTypes}, >=0;
var total_costs;
There is one significant factor in our model, which restricts which diets are acceptable, and this
is total nutritional content. This is a constraint for each nutrient. The total amount contained in
the selected diet must be added up, and this must not be less than the minimal requirement for
that particular nutrient.
Also, there is another constraint for calculating the auxiliary variable denoting the total food
costs.
s.t. Nutrient_Requirements {n in Nutrients}:
sum {f in FoodTypes} Content[f,n] * eaten[f] >= Requirement[n];
s.t. Total_Costs_Calc: total_costs =
sum {f in FoodTypes} Food_Cost[f] * eaten[f];
With the auxiliary total_costs variable, defining the objective is straightforward.
minimize Total_Costs: total_costs;
After the solve statement, again we can write our own printing code that will show the solution
found by the solver. This time, the amount for each food in the diet are shown, as well as the total
consumption per nutrient, together with the lower limit. Our model section is now ready.

62

5.5. DIET PROBLEM

PRODUCTION PROBLEM

set FoodTypes;
set Nutrients;
param Food_Cost {f in FoodTypes}, >=0, default 0;
param Content {f in FoodTypes, n in Nutrients}, >=0, default 0;
param Requirement {n in Nutrients}, >=0, default 0;
var eaten {f in FoodTypes}, >=0;
var total_costs;
s.t. Nutrient_Requirements {n in Nutrients}:
sum {f in FoodTypes} Content[f,n] * eaten[f] >= Requirement[n];
s.t. Total_Costs_Calc: total_costs =
sum {f in FoodTypes} Food_Cost[f] * eaten[f];
minimize Total_Costs: total_costs;
solve;
printf "Total Costs: %g\n", total_costs;
param Nutrient_Intake {n in Nutrients} :=
sum {f in FoodTypes} Content[f,n] * eaten[f];
for {f in FoodTypes}
{
printf "Eaten of %s: %g\n", f, eaten[f];
}
for {n in Nutrients}
{
printf "Requirement %g of nutrient %s done with %g\n",
Requirement[n], n, Nutrient_Intake[n];
}
end;
Now we also implement the particular problem mentioned. It is described in a single data section
as follows.
data;
set FoodTypes := F1 F2 F3 F4 F5;
set Nutrients := N1 N2 N3 N4;
param Food_Cost :=
F1 450
F2 220
F3 675
F4 120

63

5.6. ARBITRARY RECIPES

F5
;

PRODUCTION PROBLEM

500

param Content:
N1
N2
F1
30 5.2
F2
20
.
F3
25
2
F4
13 3.6
F5
19 0.1
;

N3
0.2
0.7
0.1
.
.

N4 :=
0.0001
0.0001
0.0001
0.0002
0.0009

param Requirement :=
N1 2000
N2 180
N3 30
N4 0.04
;
end;
Now we can solve the problem and the result is the following.
Total Costs: 29707.2
Eaten of F1: 0
Eaten of F2: 42.8571
Eaten of F3: 0
Eaten of F4: 49.2014
Eaten of F5: 28.7489
Requirement 2000 of nutrient N1 done with 2042.99
Requirement 180 of nutrient N2 done with 180
Requirement 30 of nutrient N3 done with 30
Requirement 0.04 of nutrient N4 done with 0.04
The optimal objective is 29707.2. This result literally means that any diet containing at least
2000 units of N1, 180 units of N2, 30 units of N3 and 0.04 units of N4, and consisting of F1 to F5,
would costs at least 29707.2, and there exists a solution to obtain exactly this number.
The optimal solution only uses F2, F4 and F5. This means that if F1 and F3 were omitted from
the problem data at all, then the optimal solution would be exactly the same. This is because
introducing a new food type only increases the freedom in the model, leaving any previously feasible
solutions as feasible afterwards, and possibly introducing new feasible solutions which utilize the
newly introduced food type. We can also observe that N1 is the only nutrient for which the consumption in the optimal diet is more than the minimal amount required, with 2042.99 units rather
than 2000. Of the other three nutrients, there is exactly enough. This suggests that the solution
shows a diet which is balanced to the edge to meet the minimum requirements while optimizing
costs.

5.6

Arbitrary recipes

The implementation of the diet problem and the production problem are surprisingly similar. This
is true for the number of sets, parameters, variables, constraints, the contents of the constraints,
64

5.6. ARBITRARY RECIPES

PRODUCTION PROBLEM

and the objective. In this part, we will show how the diet problem can be regarded as a production
problem. Finally, we will show a production problem with arbitrary recipes that generalizes both
problems at the same time.
There are two ways a diet problem can be represented as a production problem.
The first way is when products represent the food types, and raw materials represent the nutrients.
This can be logical in reality as well. The food can be treated as if it were „produced” from its
nutrients, in given ratios. In the diet, we eat the products and decompose it to its nutrients, so
the process is just reversed in time. The products have costs, as foods, and the amounts exactly
define the solution. The only difference is that instead of „storage” amounts for raw materials, which
serve as an upper bound for usage, we have a lower bound because each nutrient must be used in
a minimal total amount in order to obtain a good diet. But this feature is already implemented
in the limits extension, in Problem 10. Another issue is that in this case, food „production” shall
be minimized instead of maximized, but that can be easily achieved if food costs are represented
by negative revenues in the model. So technically we would have no difficulty in rewriting our diet
problem into a production problem with limits and negative unit revenues. This works if we consider
the food type as the products and the nutrients as the raw materials.
However, there is another representation which suggests a more general model behind both the
production problem and the diet problem. The second way is when products represent the nutrients,
and raw materials represent the food types. This matches the process in time: the foods are the
„inputs” which are available first, and then the products are the nutrients which we want to obtain by
the whole process. There are more differences in this representation for which it may seem unnatural.
There are no upper or lower limits for foods, but there is a lower limit for nutrients, which means
there would be a minimum production amount for each product in the production problem. There
are no costs for the nutrients, but for the foods, which means that in the production problem there
would be only costs for the raw materials, but zero revenues for all products.
But the most important difference, which actually prevents us in this second representation to
utilize the production problem model directly: the logic of production is reversed. That means, in
the production problem there are many raw materials producing a single product, in given ratios.
However, in the case of the diet problem there would be a single raw material (food) producing many
products, in given ratios.
So at this point we could say that the second representation is erroneous, and we therefore
use only the first one. Technically nothing prevents us from doing that. However, the second
representation suggests a valuable generalization for the production problem itself: what if we relax
the rule that there is only a single product in each production step? This leads to the production
problem with arbitrary recipes.
A recipe describes a process that consumes several inputs at once, and produces several outputs
at once, in given amounts. Each recipe can be utilized in an arbitrary volume, and both inputs
and outputs are proportionally sized according to this volume.
We can see that this concept of recipes may describe the production problem as well as the diet
problem.
• In the production problem, there is one recipe for each product. That recipe only produces
that particular product as output, but can consume any given combination of raw materials
as inputs.
• In the diet problem, there is one recipe for each food type. That recipe consumes only that
particular food type as input, but can produce any given combination of products as outputs.
• Moreover, there may be other problems where there are many inputs and many outputs at
the same time in a single recipe. These are covered neither by the production nor the diet
problem.

65

5.6. ARBITRARY RECIPES

PRODUCTION PROBLEM

Now we can define the production problem with arbitrary recipes as follows.
