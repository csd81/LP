---------------
Problem 24.
Solve Problem 21, the transportation problem, with one modification: there are two unit costs for
transportation, one below a given threshold amount transported, and a higher unit cost for surplus
amounts above that threshold.
The example problem to be solved here is the following.

Problem 25.
Solve Problem 22, the original example transportation problem, with one modification: the given
unit costs are only for transportation amounts below 100 units. Above that limit, costs are increased
by 25% per material units transported.
Schematically, the cost function (in contrast with the simple linear case) can be represented as in
Figure 3. Again, the region above the red curve denoting the total costs can be termed as feasible,
because we can pay more if we want, it is just not advantageous and therefore will never happen.
In the example problem, all thresholds and increased cost are uniform across the connections.
Note that this is not necessarily a case. In other problem definitions, each connection may have
unique thresholds, basic and increased unit costs.
The first thing to do is to define the data in the model needed to calculate the alternative cost
function. Two parameters are introduced: CostThreshold is the amount over which the unit costs
increase, and CostIncPercent denotes the rate of increase, in percent. Note that the increase must
be positive.
param CostThreshold, >=0;
param CostIncPercent, >0;
In the particular example, Problem 25 to be solved, the following values can be given in the data
99

6.3. INCREASING UNIT COSTS

TRANSPORTATION PROBLEM

Figure 3: Transportation costs with an increased unit cost above a threshold.
section.
param CostThreshold := 100;
param CostIncPercent := 25;
We introduce another parameter CostOverThreshold, which calculates the increased unit cost
by the following formula. Note that the original unit cost is Cost[s,d].
param CostOverThreshold {(s,d) in Connections} :=
Cost[s,d] * (1 + CostIncPercent / 100);
At this point, we have all the required data defined in the model. The question is how we could
implement the correct calculation of total costs, regardless of being below or above the threshold.
First, we can simply introduce two variables, tranBase for the amounts below, and tranOver for
the amounts above the threshold.
var tranBase {(s,d) in Connections}, >=0, <=CostThreshold;
var tranOver {(s,d) in Connections}, >=0;
Note that both quantities are nonnegative. The amount to be transported over the threshold
for the increased cost is unlimited, however, we can only transport a limited amount for the original
cost. Therefore, tranBase gets an upper bound: the threshold itself. Note that the decision of
transported amounts are individual for each connection, therefore all these variables are defined for
each connection.
The amounts represented by tranBase and tranOver are just a separation of the total amount
represented by tran. Therefore a constraint is provided to ensure that the sum of the former two
equals tran, for each connection.

100

6.3. INCREASING UNIT COSTS

TRANSPORTATION PROBLEM

s.t. Total_Transported {(s,d) in Connections}:
tran[s,d] = tranBase[s,d] + tranOver[s,d];
Instead of using tran in the objective, we can refer to the tranBase and tranOver part
separately and multiply these amounts with their corresponding unit costs, which are Cost and
CostOverThreshold.
minimize Total_Costs: sum {(s,d) in Connections}
(tranBase[s,d] * Cost[s,d] + tranOver[s,d] * CostOverThreshold[s,d]);
Our model is ready, but think about one thing. Now, the tranBase and tranOver amounts can
be freely set. For example, if the threshold is 100, then transporting 130 units in total can be done
as tranBase=100 and tranOver=30, but also for example as tranBase=50, tranOver=80. In the
latter case, the total cost is not calculated correctly in the objective, but it is still allowed by the
constraints.
But the model is still working, because the unit cost above the threshold is strictly higher than
the cost below it. Therefore the optimal solution would not attempt transporting any amounts over
the threshold unless all possible amount is transported below it, i.e. tranBase=100 in the example.
Therefore spending more is allowed as feasible solutions in the model, but these cases are eliminated
by the optimization procedure. Consequently, in the optimal solution, there is either zero amount
above the threshold, or full amount below it, for each connection. The costs are calculated correctly
in both scenarios.
The full model section for the transportation problem with increasing rates is shown here.
set Supplies;
set Demands;
param Available {s in Supplies}, >=0;
param Required {d in Demands}, >=0;
set Connections := Supplies cross Demands;
param Cost {(s,d) in Connections}, >=0;
param CostThreshold, >=0;
param CostIncPercent, >0;
param CostOverThreshold {(s,d) in Connections} :=
Cost[s,d] * (1 + CostIncPercent / 100);
check sum {s in Supplies} Available[s] >= sum {d in Demands} Required[d];
var tran {(s,d) in Connections}, >=0;
var tranBase {(s,d) in Connections}, >=0, <=CostThreshold;
var tranOver {(s,d) in Connections}, >=0;
s.t. Availability_at_Supply_Points {s in Supplies}:
sum {d in Demands} tran[s,d] <= Available[s];
s.t. Requirement_at_Demand_Points {d in Demands}:
sum {s in Supplies} tran[s,d] >= Required[d];

101

6.4. ECONOMY OF SCALE

TRANSPORTATION PROBLEM

s.t. Total_Transported {(s,d) in Connections}:
tran[s,d] = tranBase[s,d] + tranOver[s,d];
minimize Total_Costs: sum {(s,d) in Connections}
(tranBase[s,d] * Cost[s,d] + tranOver[s,d] * CostOverThreshold[s,d]);
solve;
printf "Optimal cost: %g.\n", Total_Costs;
for {(s,d) in Connections: tran[s,d] > 0}
{
printf "From %s to %s, transport %g=%g+%g " &
"amount for %g (unit cost: %g/%g).\n",
s, d, tran[s,d], tranBase[s,d], tranOver[s,d],
(tranBase[s,d] * Cost[s,d] + tranOver[s,d] * CostOverThreshold[s,d]),
Cost[s,d], CostOverThreshold[s,d];
}
Solving the model with a value of 100 for CostThreshold and 25 for CostIncPercent, we get
the following result.
Optimal cost: 2772.5.
From S1 to D5, transport 100=100+0 amount for 500 (unit cost: 5/6.25).
From S2 to D1, transport 120=100+20 amount for 125 (unit cost: 1/1.25).
From S2 to D2, transport 130=100+30 amount for 275 (unit cost: 2/2.5).
From S3 to D2, transport 10=10+0 amount for 50 (unit cost: 5/6.25).
From S3 to D3, transport 170=100+70 amount for 187.5 (unit cost: 1/1.25).
From S3 to D5, transport 10=10+0 amount for 40 (unit cost: 4/5).
From S4 to D4, transport 90=90+0 amount for 720 (unit cost: 8/10).
From S4 to D6, transport 120=100+20 amount for 875 (unit cost: 7/8.75).
The optimal solution is 2772.5 units, and in some cases the thresholds are surpassed. The
objective is slightly worse than the original 2700. This is not surprising, because the current and
original problems only differ in the increased costs. If we closely look at the transportation amounts,
we can observe that the increased unit costs over the 100 threshold do not only change the pricing,
but the optimal transportation decisions as well.

6.4

Economy of scale

The previous Section 6.3 deals with the case of increased unit costs, but what happens when the
unit costs do not increase, but decrease above the threshold?
The case when the larger amounts provide lower unit prices is called the economy of scale.
This is also common in practice. In production environments, if more products are to be processed,
the same investment and/or operating costs might share between more products, therefore the cost
per product decreases, making production in larger volumes cost-efficient. In trade, it is possible
we get a discount if we purchase in a larger volume.
Here, the simplest case of economy of scale is presented, where unit costs simply decrease above
a threshold. As usual, we provide a general problem definition and an example problem.

102

6.4. ECONOMY OF SCALE

TRANSPORTATION PROBLEM

