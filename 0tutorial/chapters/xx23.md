

## Problem 28.

Solve Problem 21, the transportation problem, with the following modification: for each connection, there is a **fixed cost** to be paid once for **establishing it**, in order to use that connection to transport materials.

The term **fixed cost** means that its value does not depend on the actual amounts in question. We only have to pay the fixed cost once to utilize a feature. In this example, the feature is transportation through each connection. We may choose **not to establish a connection**, but then we cannot transport anything between that particular supply and demand point.

The example problem to be solved here is the following.

-----

## Problem 29.

Solve Problem 28, the transportation problem with fixed costs, using data from the original example **Problem 22**, and the following fixed establishment costs per connection.

| | **D1** | **D2** | **D3** | **D4** | **D5** | **D6** |
| :---: | :---: | :---: | :---: | :---: | :---: | :---: |
| **S1** | 50 | 50 | 80 | 50 | 200 | 50 |
| **S2** | 200 | 50 | 50 | 50 | 200 | 50 |
| **S3** | 50 | 200 | 200 | 50 | 50 | 50 |
| **S4** | 50 | 50 | 50 | 200 | 200 | 200 |

The starting point for implementing our model is the general implementation from **Section 6.2** where all connections were present. We add a new parameter, **`FixedCost`**, for the establishment costs per connection. The matrix in Problem 29 can then be implemented in the data section to provide values for `FixedCost`.

```
param FixedCost {(s,d) in Connections}, >=0;
```

```
param FixedCost:
D1 D2 D3 D4 D5 D6 :=
S1 50 50 80 50 200 50
S2 200 50 50 50 200 50
S3 50 200 200 50 50 50
S4 50 50 50 200 200 200
;
```

If we observe the schematic representation of the fixed cost (**Figure 5**), we can see that the curve is again **not convex**, as in the case of the economy of scale.  The non-convex nature comes from the single point where the total amount and the total cost are zero, a single point which cannot be directly connected to the horizontal line describing the fixed cost for any amounts above zero without intersecting the infeasible region.

As in the case of the economy of scale, we need a **binary decision variable** to distinguish the two cases. Here, **`tranUsed`** equals one if we choose to establish the connection and are able to send any positive amount of materials over it. If `tranUsed` is zero, then transportation on that particular connection **must also be zero**.

```
var tranUsed {(s,d) in Connections}, binary;
```

Two things must be enforced regarding the fixed cost and the `tranUsed` variable. The first is that if we **do not establish the connection** ($\text{tranUsed}=0$), then there must be **no amount transported** there ($\text{tran}=0$). This can be done with a single **big-M constraint**.

```
param M := sum {s in Supplies} Available[s];
s.t. Zero_Transport_If_Fix_Cost_Not_Paid {(s,d) in Connections}:
tran[s,d] <= M * tranUsed[s,d];
```

The coefficient $M$ must be large in order not to exclude legitimate solutions from the search space. Precisely, it must be at least the largest possible value of $\text{tran}[s,d]$ in the optimal solution to be found. The total of all available supplies, denoted by $M$, is a single valid candidate for all big-M constraints, for any supply and demand pair. However, if we want **stricter constraints** that might result in better solver running times, we can provide a particular value for each supply and demand, which is the **minimum of the available amount at the supply and the required amount at the demand**. There cannot be more traffic between the two points than this minimum.

```
s.t. Zero_Transport_If_Fix_Cost_Not_Paid {(s,d) in Connections}:
tran[s,d] <= min(Available[s],Required[d]) * tranUsed[s,d];
```

The second thing to implement is the inclusion of the fixed establishment cost into the **objective function**.

```
minimize Total_Costs: sum {(s,d) in Connections}
(tran[s,d] * Cost[s,d] + tranUsed[s,d] * FixedCost[s,d]);
```

Our implementation is ready. The variable $\text{tranUsed}[s,d]$ is either zero, in which case there is no fixed cost and no transportation over that connection, or $\text{tranUsed}[s,d]$ is one, the cost is paid, and there can be a positive transportation amount over that connection. Simultaneously zero transportation while still establishing the connection ($\text{tranUsed}[s,d]=1$ and $\text{tran}[s,d]=0$) is allowed as a feasible solution in the model, but the optimization procedure rules these cases out. The full model section is the following.

### Full MILP Model for Fixed Costs

```
set Supplies;
set Demands;
param Available {s in Supplies}, >=0;
param Required {d in Demands}, >=0;
set Connections := Supplies cross Demands;
param Cost {(s,d) in Connections}, >=0;
param FixedCost {(s,d) in Connections}, >=0;
check sum {s in Supplies} Available[s] >= sum {d in Demands} Required[d];
var tran {(s,d) in Connections}, >=0;
var tranUsed {(s,d) in Connections}, binary;
s.t. Availability_at_Supply_Points {s in Supplies}:
sum {d in Demands} tran[s,d] <= Available[s];
s.t. Requirement_at_Demand_Points {d in Demands}:
sum {s in Supplies} tran[s,d] >= Required[d];
s.t. Zero_Transport_If_Fix_Cost_Not_Paid {(s,d) in Connections}:
tran[s,d] <= min(Available[s],Required[d]) * tranUsed[s,d];
minimize Total_Costs: sum {(s,d) in Connections}
(tran[s,d] * Cost[s,d] + tranUsed[s,d] * FixedCost[s,d]);
solve;
printf "Optimal cost: %g.\n", Total_Costs;
for {(s,d) in Connections: tran[s,d] > 0}
{
printf "From %s to %s, transport %g " &
"amount for %g (unit cost: %g, fixed: %g).\n",
s, d, tran[s,d],
tran[s,d] * Cost[s,d] + FixedCost[s,d], Cost[s,d], FixedCost[s,d];
}
end;
```

The original **Problem 22** with zero fixed costs and the new **Problem 29** mentioned here are both solved by the model. The original optimal solution is **2,700**, and this is reproduced by the model with fixed costs if these fixed costs are simply zero. If nonzero fixed costs are present, the following output is reported.

```
Optimal cost: 3640.
From S1 to D1, transport 100 amount for 550 (unit cost: 5, fixed: 50).
From S2 to D1, transport 20 amount for 220 (unit cost: 1, fixed: 200).
From S2 to D2, transport 140 amount for 330 (unit cost: 2, fixed: 50).
From S2 to D4, transport 90 amount for 140 (unit cost: 1, fixed: 50).
From S3 to D3, transport 80 amount for 280 (unit cost: 1, fixed: 200).
From S3 to D5, transport 110 amount for 490 (unit cost: 4, fixed: 50).
From S4 to D3, transport 90 amount for 590 (unit cost: 6, fixed: 50).
From S4 to D6, transport 120 amount for 1040 (unit cost: 7, fixed: 200).
```

The objective is **3,640**. The connections used are **not the same** in the two cases (zero fixed costs vs. the fixed costs from Problem 29). The total establishment cost paid is **850**, which is smaller than the increment of 940 in the objective ($3,640 - 2,700 = 940$). This is natural, because not only is the extra fixed cost paid, but the new transportation solution obtained is also **not necessarily optimal** according to the original, pure unit-cost objective anymore.

-----

## 6.6 Flexible Demands

Several transportation cost functions were introduced, including increasing, decreasing proportional, and fixed costs. One additional example is mentioned now, but in a slightly different context.

The common property of transportation costs was that total cost **increased** (or remained constant) as the total transported amount increased. But what happens when the curve is **not monotonically increasing**? Since this is not very practical for transportation costs, we will instead demonstrate it through the **demands**. So far, the required amount at each demand point was a **strict constraint**. This means that any solutions for the problem in which demands are not met are **infeasible** and are eliminated from the search. All constraints so far were strict.

In some cases, constraints are **soft constraints** instead. This means we are interested in solutions that **violate them**. In that case, the **extent of the violation** must be minimized, which becomes another objective of the optimization procedure. Considering multiple objectives falls under the field of **goal programming** [17], and is not covered here. However, there are techniques to **merge different objective functions into a single one**, resulting in an LP/MILP model again. One technique is the usage of **penalties**, which are terms added to the objective function and are proportional to the extent a constraint is violated. Penalties are practical: failing to meet a desired goal can sometimes be measured or estimated as **costs**, which can be added to the objective if it is profit maximization or cost minimization.

In this section, the starting point is the original transportation problem again, and it is expanded with **flexible demands**.

