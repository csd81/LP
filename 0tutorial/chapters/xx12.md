---------------
Problem 12.
The minimum production in a production problem refers to the product from which the least
amount is produced. Maximize the minimum production while problem data are the same.
Regardless of what model we take as a starting point, the simple one or the one with the
limitations added, this problem only wants us to modify the objective function. That means, the
feasible solutions remain exactly the same as before, and the search space does not change.
This also makes the revenue parameter in the problem irrelevant, because we are no longer
interested in how much the products worth in total, only the amounts produced (moreover, only
the minimum of those amounts).
We have already done a similar problem in case of the system of linear equations (see Problem
7 from Section 4.8). Then the maximum of the errors of all equations was the objective to be
minimized. Now, the minimum of the productions is to be maximized. The modeling technique
required is indeed similar.
The idea is that we introduce a variable for the minimum of the production amounts. Precisely,
the variable will denote a lower bound for all the production amounts. That works as a lower bound
for the minimum of those amounts as well. If this variable is maximized, then by the optimization,
it will eventually go as large as possible, until it reaches the minimum of the production amounts.
With this modeling trick, we can ensure that the final optimal solution found will be the maximal
possible value of the minimum of the production amounts. Of course, this is a more general method
that works on any number of linear expressions, a minimum of which is to be maximized, or a
maximum of which is to be minimized.
In implementation, parameters and post-processing work remains the same, as well as the
already-existing variables, bounds and constraints.
var production {p in Products}, >=Min_Production[p], <=Max_Production[p];
var usage {r in Raw_Materials}, >=Min_Usage[r], <=Storage[r];
var total_revenue;
s.t. Usage_Calc {r in Raw_Materials}:
sum {p in Products} Consumption_Rate[r,p] * production[p] = usage[r];
s.t. Total_Revenue_Calc: total_revenue =
sum {p in Products} Revenue[p] * production[p];
The change is that instead of the original objective of the total revenue, we set the minimum
production as objective. This requires a newly introduced min_production variable to denote a

55

5.3. MAXIMIZING MINIMUM PRODUCTION

PRODUCTION PROBLEM

lower bound for all the production amounts, and a constraint which ensures that the variable is
itself a lower bound. Then, the variable is to be maximized, and that is the objective function.
var min_production;
s.t. Minimum_Production_Calc {p in Products}:
min_production <= production[p];
maximize Minimum_Production: min_production;
Post-processing code can be the same as before, so we print the same data as we did for the
total revenue objective. We only added a printf statement to emphasize the minimum production
amount, which is our current objective.
printf "Minimum Production: %g\n", min_production;
Note that in our code, min_production is the name of the variable, and Min_production is
the name of the objective function. In GNU MathProg, we could use both as values. However,
be cautious when referring to the objective by its name, because constant terms in the objective
are omitted. The reason behind this is that the selection of the optimal solution is not affected
by constant terms, just the value of the objective. Therefore, it is recommended to refer to the
objective function value by the variable min_production instead.
We can now run our model to solve two problems. In the first one, all the limits are ignored,
except the Storage. Note that, in the data section, we can start rows by a hash mark (#). This
makes the row a comment, effectively excluding it from processing. The data section looks like the
following.
data;
set Products := P1 P2 P3;
set Raw_Materials := A B C D;
param Storage :=
A 23000
B 31000
C 450000
D 200
;
param Consumption_Rate:
P1
P2
P3 :=
A
200
50
0
B
25
180
75
C 3200 1000 4500
D
1
1
1
;
param Revenue :=
P1 252
P2 89
P3 139

56

5.3. MAXIMIZING MINIMUM PRODUCTION

PRODUCTION PROBLEM

;
param Min_Usage :=
# B 21000
# D 200
;
param Min_Production :=
# P2 100
;
param Max_Production :=
# P3 10
;
end;
Note that revenue data are not needed at all in this model, but it is not a mistake to give them
values as well, as long as the parameter is represented in the model section.
The solution to the problem is a production amount of 51.72.
Total Revenue: 24827.6
Minimum Production: 51.7241
Production of P1: 51.7241
Production of P2: 51.7241
Production of P3: 51.7241
Usage of A: 12931, remaining: 10069
Usage of B: 14482.8, remaining: 16517.2
Usage of C: 450000, remaining: 0
Usage of D: 155.172, remaining: 44.8276
It turns out that production is balanced to the edge to achieve this solution. All the resources
are evenly distributed, and all three products are produced in an equal amount. If we think about it,
that is not surprising. Why would we produce any amount of products above the minimum, if it is
not an advantage regarding the objective function, but a disadvantage regarding the raw materials
consumed?
It looks like raw material C is the bottleneck in this problem. We call a factor a bottleneck if
that factor has a visible impact on the final solution if changed, while other factors remained the
same. For example, the most scarce resource, as in this case. If there were slightly less or more
of raw materials A, B and D, the solution were the same, because all of C are completely used up,
and distributed evenly among the products. This kind of knowledge can be fundamental in realworld optimization problems, because it tells decision makers that some factors are unnecessary to
improve. On the other hand, slightly more or less C would probably result in slightly more or less
production (probably, because there can be other limitations in the model that we do not see). For
this reason raw material C is the bottleneck in this particular production problem.
In the second example, the Problem 11 is used without the constraint on P3. That means, all
the limitations are included now in the model, except that P3 is not maximized in 10 units. Note
that if this constraint were enabled, then there is no question that the optimal solution would be
10 units, as the former solution we already know just produces 10 units of P3, and there cannot be
more.

57

5.4. RAW MATERIAL COSTS AND PROFIT

PRODUCTION PROBLEM

In short, we only get a meaningful new problem if we omit this constraint about P3 maximized
at 10 units.
Total Revenue: 27142.1
Minimum Production: 43.8596
Production of P1: 43.8596
Production of P2: 112.281
Production of P3: 43.8596
Usage of A: 14386, remaining: 8614.04
Usage of B: 24596.5, remaining: 6403.51
Usage of C: 450000, remaining: 0
Usage of D: 200, remaining: 0
The solution is slightly different in this case. Now only P1 and P3 is produced in even amounts,
both in the minimum production amount of 43.86. Also, not only C, but D is also used up totally.
Note that there is a need of P2 much more because all 200 units of D must be used up in this
problem. And this answers a former question: why is it an advantage to produce products above
the minimum limit? Because it might help satisfying the minimum production and/or minimum
consumption constraints.
Note that the objective in this case is slightly worse than that of the first example problem, where
the objective was 51.72. This is a natural consequence of differences in the data. In the second
example, the data were the same, except that there were additional constraints on consumption and
production. If a problem is more constrained, we can only get an optimal solution with the same,
or a worse objective value.
Generally, if only the objective is changed in a model, then the set of feasible solutions remains the same, because the objective only guides in selecting the most suitable solution, but not
in which solutions are feasible. In this case, the new objective function involved a new variable
min_production, which served as a lower bound and was maximized.
Note that, in theory, the variable min_production does not need any lower bound, and can be
allowed to even be zero or negative. The solution would still be feasible. However, such solutions are
not reported because they are not optimal. Therefore only those feasible solutions are interesting
for which min_production is not only a valid lower bound on production amounts, but is actually
strict, that is, equals the minimum production amount.
And among these solutions we are interested in, min_production works just like an auxiliary
variable. Each optimal solution where min_production is a strict bound corresponds to a feasible
solution of the original problem where revenues were maximized, and vice versa.

5.4

Raw material costs and profit

We have seen an example where only the objective was changed. Now let us see another example,
which is more like a natural extension to the problem. From now on, raw materials are not considered
„free” anymore: they must be produced, purchased, stored, etc. in general, they have costs. As there
is a revenue for each product per unit produced, there is now a cost for each raw material per unit
consumed.
