---------------
Problem 32.
Solve Problem 21, the transportation problem extended with a middle level of so-called centers
introduced.
Material does not go directly from the supply to the demand. Instead, material goes from supply
nodes to center nodes first, and then from center nodes to demand nodes. Connections between
either supplies and centers, or centers and demands, have their own transportation costs as usual.
Moreover, centers are not necessarily used in transportation, but if are, an establishment cost
must be paid first if any transportation is planned there. Establishment costs are one-time, and given
for each center.
As already mentioned, the original transportation problem can be represented by a graph with
two types of nodes: supplies and demands (see Figure 1 in Section 6.1). In contrast, the scheme of
the new transportation problem can be seen in Figure 7. Connections from supplies to centers are
termed as A-type, while connections from centers to demands are termed as B-type, as the Figure
shows.
Note that in general, there could be any network for transportation. However, if cycles are
present, care must be taken to correctly interpret material flow through these cycles. Throughout
this chapter, there are no cycles, material only flows in one direction.

Figure 7: Graph representing the transportation problem with center nodes included.
 
Problem 33.
Solve Problem 21, the general transportation problem with centers, with the following problem
data. There are four supplies, named S1 to S4, two centers, C1 and C2, and six demands, named D1
to D6. Transportation costs, available amounts for supplies, and required amounts for demands are
summarized in the following table.

C1
C2
Amounts

S1
5
10
100

S2
10
2
250

S3
3
7
190

S4
10
4
210

D1
10
2
120

116

D2
3
7
140

D3
9
3
170

D4
8
2
90

D5
1
7
110

D6
6
8
120

6.7. ADDING A MIDDLE LEVEL

TRANSPORTATION PROBLEM

Two scenarios must be evaluated.
• In the first, „small” establishment cost scenario, using C1 and C2 require a fixed cost of 1200
and 1400, respectively.
• In the second, „large” establishment cost scenario, fixed costs are multiplied by 10, e.g. 12000
and 14000, respectively.
Before model implementation starts, the data section describing Problem 33 can be implemented
as follows. The only difference of the two scenarios is in the value of the EstablishCost parameter.
data;
set Supplies := S1 S2 S3 S4;
set Centers := C1 C2;
set Demands := D1 D2 D3 D4 D5 D6;
param Available :=
S1 100
S2 250
S3 190
S4 210
;
param Required :=
D1 120
D2 140
D3 170
D4 90
D5 110
D6 120
;
param CostA:
C1 C2 :=
S1
5 10
S2 10
2
S3
3
7
S4 10
4
;
param CostB:
D1 D2
C1 10
3
C2
2
7
;

D3
9
3

D4
8
2

D5
1
7

D6 :=
6
8

param EstablishCost :=
C1 1200

117

6.7. ADDING A MIDDLE LEVEL

C2
;

TRANSPORTATION PROBLEM

1400

Sets Supplies and Demands, and parameters Available and Required have the same meaning as
before. However, instead of a single parameter for transportation costs, two parameters named CostA
and CostB are introduced for A-type and B-type connections, respectively. Finally, EstablishCost
denotes the fixed cost of using a center node.
The corresponding definitions in the model section are the following. Note that the sets of all
A-type and B-type connections are also defined.
set Supplies;
set Centers;
set Demands;
param Available {s in Supplies}, >=0;
param Required {d in Demands}, >=0;
set ConnectionsA := Supplies cross Centers;
set ConnectionsB := Centers cross Demands;
param CostA {(s,c) in ConnectionsA}, >=0;
param CostB {(c,d) in ConnectionsB}, >=0;
param EstablishCost {c in Centers}, >=0;
We can still check feasibility of the problem by asserting that the available amounts are larger
than required amounts in total.
check sum {s in Supplies} Available[s] >= sum {d in Demands} Required[d];
Now the data are available in the model section, let us find out what the decision variables in
the model shall be. At first glance, the problem seems like two independent transportation subproblems: one from supplies to centers, and one from centers to demands. However, we cannot solve
these two transportation stages separately, because the amounts at the center nodes are not fixed:
these are part of the decision.
However, we can solve the two transportation sub-problems in a single model. The necessary
decision variables for both transportation problems are simultaneously defined in the model: from
supplies to centers, and from centers to demands. The only difference is that the amount of materials delivered to, and sent from the centers are not fixed values in the sub-problems, but variable
quantities. Moreover, the amount delivered to and from a center node shall be equal. This is where
the two sub-problems are connected, and can be easily established by constraints.
Because of these considerations, variables tranA and tranB are introduced to denote transportation amounts for connections of A-type and B-type, e.g. from supplies to centers and from centers
to demands. In fact, these decisions are sufficient to determine everything about the resulting
transportation system, however, additional variables are required. Variable atCenter denotes the
amount of material going through each center node, which is exactly the amount going in, and going
out. This is rather an auxiliary variable as not strictly required but makes implementation easier.
However, binary variable useCenter denoting whether a center is used at all (= 1) or not (= 0) is
required in order to implement the fixed establishment costs associated with center nodes.

118

6.7. ADDING A MIDDLE LEVEL

TRANSPORTATION PROBLEM

var tranA {(s,c) in ConnectionsA}, >=0;
var tranB {(c,d) in ConnectionsB}, >=0;
var atCenter {c in Centers}, >=0;
var useCenter {c in Centers}, binary;
Four constraints ensure the consistency of material flow through the whole network. In order,
the following rules are expressed.
1. Transportation from supply nodes in total shall not exceed the availability.
2. Transportation to center nodes in total is equal to the atCenter value of that center node.
3. Transportation from center nodes in total is equal to the atCenter value of that center node.
4. Transportation to demand nodes in total shall be at least the required amount.
s.t. Availability_at_Supply_Points {s in Supplies}:
sum {c in Centers} tranA[s,c] <= Available[s];
s.t. Total_To_Center {c in Centers}:
atCenter[c] = sum {s in Supplies} tranA[s,c];
s.t. Total_From_Center {c in Centers}:
atCenter[c] = sum {d in Demands} tranB[c,d];
s.t. Requirement_at_Demand_Points {d in Demands}:
sum {c in Centers} tranB[c,d] >= Required[d];
Note that as we express atCenter in two different ways, the two expressions that are equal to
atCenter are made equal to each other.
A fifth constraint must be added to ensure that a center node can only receive or send materials
if it established. The total availability is denoted by M which serves as the coefficient in the required
big-M constraint.
param M := sum {s in Supplies} Available[s];
s.t. Zero_at_Center_if_Not_Established {c in Centers}:
atCenter[c] <= M * useCenter[c];
The objective is the total of all costs, which has three components: A-type, B-type connections,
and establishment costs of center nodes.
minimize Total_Costs:
sum {(s,c) in ConnectionsA} tranA[s,c] * CostA[s,c] +
sum {(c,d) in ConnectionsB} tranB[c,d] * CostB[c,d] +
sum {c in Centers} useCenter[c] * EstablishCost[c];
We can include printf statements after the solve statement as usual to show the details of the
optimal solution found. Our model section is ready.

119

6.7. ADDING A MIDDLE LEVEL

TRANSPORTATION PROBLEM

set Supplies;
set Centers;
set Demands;
param Available {s in Supplies}, >=0;
param Required {d in Demands}, >=0;
set ConnectionsA := Supplies cross Centers;
set ConnectionsB := Centers cross Demands;
param CostA {(s,c) in ConnectionsA}, >=0;
param CostB {(c,d) in ConnectionsB}, >=0;
param EstablishCost {c in Centers}, >=0;
check sum {s in Supplies} Available[s] >= sum {d in Demands} Required[d];
var tranA {(s,c) in ConnectionsA}, >=0;
var tranB {(c,d) in ConnectionsB}, >=0;
var atCenter {c in Centers}, >=0;
var useCenter {c in Centers}, binary;
s.t. Availability_at_Supply_Points {s in Supplies}:
sum {c in Centers} tranA[s,c] <= Available[s];
s.t. Total_To_Center {c in Centers}:
atCenter[c] = sum {s in Supplies} tranA[s,c];
s.t. Total_From_Center {c in Centers}:
atCenter[c] = sum {d in Demands} tranB[c,d];
s.t. Requirement_at_Demand_Points {d in Demands}:
sum {c in Centers} tranB[c,d] >= Required[d];
param M := sum {s in Supplies} Available[s];
s.t. Zero_at_Center_if_Not_Established {c in Centers}:
atCenter[c] <= M * useCenter[c];
minimize Total_Costs:
sum {(s,c) in ConnectionsA} tranA[s,c] * CostA[s,c] +
sum {(c,d) in ConnectionsB} tranB[c,d] * CostB[c,d] +
sum {c in Centers} useCenter[c] * EstablishCost[c];
solve;
printf "Optimal cost: %g.\n", Total_Costs;
for {c in Centers: useCenter[c]}
{
printf "Establishing center %s for %g.\n", c, EstablishCost[c];

120

6.7. ADDING A MIDDLE LEVEL

TRANSPORTATION PROBLEM

}
for {(s,c) in ConnectionsA: tranA[s,c] > 0}
{
printf "A: From %s to %s, transport %g amount for %g (unit cost: %g).\n",
s, c, tranA[s,c], tranA[s,c] * CostA[s,c], CostA[s,c];
}
for {(c,d) in ConnectionsB: tranB[c,d] > 0}
{
printf "B: From %s to %s, transport %g amount for %g (unit cost: %g).\n",
c, d, tranB[c,d], tranB[c,d] * CostB[c,d], CostB[c,d];
}
end;
Now we can use the solver to find the optimal decisions for both scenarios in Problem 33. Recall
that the small scenario assumes fixed costs of 1200 and 1400 for establishing C1 and C2, while the
large scenario assumes 12000 and 14000.
The results of the small scenario are the following.
Optimal cost: 7350.
Establishing center C1 for 1200.
Establishing center C2 for 1400.
A: From S1 to C1, transport 100 amount for 500 (unit cost: 5).
A: From S2 to C2, transport 250 amount for 500 (unit cost: 2).
A: From S3 to C1, transport 190 amount for 570 (unit cost: 3).
A: From S4 to C2, transport 210 amount for 840 (unit cost: 4).
B: From C1 to D2, transport 140 amount for 420 (unit cost: 3).
B: From C1 to D5, transport 110 amount for 110 (unit cost: 1).
B: From C1 to D6, transport 40 amount for 240 (unit cost: 6).
B: From C2 to D1, transport 120 amount for 240 (unit cost: 2).
B: From C2 to D3, transport 170 amount for 510 (unit cost: 3).
B: From C2 to D4, transport 90 amount for 180 (unit cost: 2).
B: From C2 to D6, transport 80 amount for 640 (unit cost: 8).
The results of the large scenario are the following.
Optimal cost: 21310.
Establishing center C2 for 14000.
A: From S1 to C2, transport 100 amount for 1000 (unit cost: 10).
A: From S2 to C2, transport 250 amount for 500 (unit cost: 2).
A: From S3 to C2, transport 190 amount for 1330 (unit cost: 7).
A: From S4 to C2, transport 210 amount for 840 (unit cost: 4).
B: From C2 to D1, transport 120 amount for 240 (unit cost: 2).
B: From C2 to D2, transport 140 amount for 980 (unit cost: 7).
B: From C2 to D3, transport 170 amount for 510 (unit cost: 3).
B: From C2 to D4, transport 90 amount for 180 (unit cost: 2).
B: From C2 to D5, transport 110 amount for 770 (unit cost: 7).
B: From C2 to D6, transport 120 amount for 960 (unit cost: 8).
As we can see, the optimal solution for the small scenario uses both center nodes, in an almost
equal distribution of materials. Therefore establishing both centers for 2600 is advantageous. There
121

6.8. TRANSPORTATION PROBLEM – SUMMARY

TRANSPORTATION PROBLEM

is no need to deliver to both center nodes from the same supply, and there is only a single demand
D6 where delivery is made from both center nodes. It seems like establishment costs are paid, and
then the cheapest possible routes are used from supplies to demands.
In contrast, the establishment costs are too high in the large scenario, so establishing both
centers is not beneficial. However, we still need at least one center node for the network, and the
optimal solution chooses C2 with a fixed cost of 14000. Assuming this decision, transportation is
straightforward: every supply is transported to C2 and then from C2 to demands. Note that C2
is more expensive than C1 by 2000, but choosing C2 seems to be justified by larger transportation
costs across C1.

6.8

Transportation problem – Summary

The basic case of the transportation problem was first presented, which involves a set of supply and
demand nodes, and transportation costs between these. The implementation was later improved by
the introduction of index sets and some additional features of GNU MathProg.
The transportation problem results in an LP model, and transportation costs for each connection
are linear, e.g. can be described by a single cost coefficient, which multiplied by the transported
amount gives the total cost. Four cases of more elaborate cost functions, example problems and
their implementations were also presented, which are the following.
• Increasing unit costs. Two factors and a threshold describe the situation, but we still use an
LP model.
• Economy of scale. Two factors and a threshold describes the function again, but the cost
above the threshold is now less. Binary decision variables are required, making it an MILP
model.
• Fixed costs. A fixed cost once paid instead of a proportional cost describes the cost function.
A binary variable is used, the model is MILP.
• Linear penalties. Flexible demands were assumed, and any difference, either positive or negative, was penalized by a linear factor. This results in an LP model.
Finally, the transportation problem was extended by center nodes, which divides the whole
problem into two related sub-problems, and a fixed cost decision problem is also involved.

122
